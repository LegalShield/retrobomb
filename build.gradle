def sharedGroupId = 'com.legalshield'
def sharedVersion = '0.0.5'

group sharedGroupId
version sharedVersion

buildscript {
    ext {
        /** build configuration */
        upload_artifacts = System.getenv("uploadRetrobomb") == "true"
        kotlin_version = '1.2.51'
        jvm_target = '1.6'

        /** test dependencies */
        junit_version = '4.12'
        hamcrest_version = '1.3'
        spectrum_version = '1.2.0'
        kluent_version = '1.28'
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

subprojects {
    apply {
        plugin 'kotlin'
        plugin 'signing'
        plugin 'maven'
    }

    repositories {
        jcenter()
    }

    group = sharedGroupId
    archivesBaseName = "retrobomb"
    version = sharedVersion

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allSource
    }

    signing {
        if (upload_artifacts) {
            sign configurations.archives
        }
    }

    artifacts {
        archives sourcesJar, javadocJar, jar
    }

    uploadArchives {
        if (upload_artifacts) {
            repositories {
                mavenDeployer {
                    beforeDeployment {
                        MavenDeployment deployment -> signing.signPom(deployment)
                    }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    pom.project {
                        name project.name
                        packaging 'jar'

                        description 'Simple error response mapping system for Retrofit and OkHttp.'
                        url 'https://github.com/LegalShield/retrobomb'

                        scm {
                            connection 'scm:git:https://github.com/LegalShield/retrobomb'
                            developerConnection 'scm:git:https://github.com/LegalShield/retrobomb'
                            url 'https://github.com/LegalShield/retrobomb'
                        }

                        licenses {
                            license {
                                name 'MIT'
                                url 'https://github.com/LegalShield/retrobomb/blob/master/LICENSE'
                            }
                        }

                        developers {
                            developer {
                                id 'core'
                                name 'LegalShield'
                                email 'opensource@legalshieldcorp.com'
                            }
                        }
                    }
                }
            }
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = jvm_target
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = jvm_target
    }
}
